import syntax_tree;
import sys/system;

export {
	simplifyTree(ar:AlgExp) -> AlgExp;
	extractArgs(ar:AlgExp) -> [AlgExp];
}

simplifyTree(ar:AlgExp) -> AlgExp {
	//transform and extract signs like x + (-y) = x-y
	it0 = foldSigns(ar);
	//Find simple expression like a^1, a+0, a*0, a-0 etc
	it1 = removeTrivialExp(it0);
	it2 = foldSigns(it1);
	//Simplify more complex expression like (x-y) + (x+y) = 2x
	it3 = foldExpressions(it2);

	if(ar == it3)
		ar
	else
		simplifyTree(it3);	
} 

foldSigns(ar:AlgExp) -> AlgExp {
	it = extractSign(ar);

	if(alg2s(it.second) == alg2s(ar)) 
		ar
	else {
		if(it.first == -1) {
			if(getD(it.second).first)
				AlgDouble(getD(it.second).second * -1.0)
			else {
				if(detectType(it.second) == "*" || detectType(it.second) == "/") {
					arr = extractArgs(it.second);
					if(getD(arr[0]).first) 
						createStruct("*", AlgDouble(getD(arr[0]).second * (-1.0)), arr[1])
					else if(getD(arr[1]).first) 
						createStruct("*", AlgDouble(getD(arr[1]).second * (-1.0)), arr[0])
					else
						AlgMult(AlgDouble(-1.0), it.second);
				}else
					AlgMult(AlgDouble(-1.0), it.second);
			}
		}else
			it.second;	
	}
}

foldExpressions(ar:AlgExp) -> AlgExp {
	it = execSimplifier(ar, \op, mAr -> {
		args = extractArgs(mAr);
		if(length(args) == 2)
		{
			p0 = foldSigns(args[0]);
			p1 = foldSigns(args[1]);
			op0 = detectType(p0);
			op1 = detectType(p1);
			arg0 = extractArgs(p0);
			arg1 = extractArgs(p1);

			if(op == "+") {
				if(op0 == "+" && op1 == "-") {
					if(arg1[1] == arg0[0])//(x+y)+(z-x) = y+z
						AlgSum(arg0[1], arg1[0])
					else if(arg1[1] == arg0[1])//(y+x)+(z-x) = y+z
						AlgSum(arg0[0], arg1[0])
					else mAr;	
				}else if(op0 == "-" && op1 == "+") {
					if(arg0[1] == arg1[0]) //(y-x)+(x+z) = y+z
						AlgSum(arg0[0], arg1[1])
					else if(arg0[1] == arg1[1]) //(y-x)+(z+x) = y+z
						AlgSum(arg0[0], arg1[0])
					else mAr;
				}else if(op0 == "-" && op1 == "-") {
					if(arg0[0] == arg1[1]) //(x-y)+(z-x) = -y+z = z-y
						AlgMin(arg1[0], arg0[1])
					else mAr;	
				}else if(isAtom(arg1[0]) || isAtom(arg0[0]))
				{
					if(op0 == "-" && arg0[1] == arg1[0]) arg0[0] // (x-y)+y = x
					else if(op1 == "-" && arg1[1] == arg0[0]) arg1[0] // y+(x-y) = x
					else mAr;	
				}else mAr;
			}else if(op == "-") {
				if(op0 == "+" && op1 == "-") {
					if(arg1[0] == arg0[0]) // (x+y)-(y-z) = x+z
						AlgSum(arg0[1], arg1[1])
					else if(arg1[0] == arg0[1]) // (y+x)-(y-z) = x+z
						AlgSum(arg0[0], arg1[1])	
					else
						mAr;	
				}else if(op0 == "-" && op1 == "+") {
					if(arg0[0] == arg1[1]) // (y-x)-(z+y) = -1 * (x+z)
						AlgMult(AlgDouble(-1.0), AlgSum(arg0[1], arg1[0]))
					else if(arg0[0] == arg1[0]) // (y-x)-(y+z) = -1 * (x+z)
						AlgMult(AlgDouble(-1.0), AlgSum(arg0[1], arg1[1]))
					else mAr;	
				}else if(op0 == "-" && op1 == "-") {
					if(arg0[1] == arg1[1]) // (x-y)-(z-y) = x-z
						AlgMin(arg0[0],arg1[0])
					else mAr;
				}else if(op0 == "+" && op1 == "+") {// (x+y)-(z+y) = x-z
					if(arg0[0] == arg1[1]) 
						AlgMin(arg0[1],arg1[0])
					else if(arg0[0] == arg1[0]) 
						AlgMin(arg0[1],arg1[1])
					else if(arg0[1] == arg1[1]) 
						AlgMin(arg0[0],arg1[0])
					else if(arg0[1] == arg1[0]) 
						AlgMin(arg0[0],arg1[1])
					else mAr;
				}else if(isAtom(arg1[0]) || isAtom(arg0[0]))
				{
					if(op0 == "+" && arg0[1] == arg1[0]) arg0[0] // (x+y)-y = x
					else if(op0 == "+" && arg0[0] == arg1[0]) arg0[1]  // (y+x)-y = x
					else if(op0 == "-" && arg0[0] == arg1[0]) AlgMult(AlgDouble(-1.0), arg0[1]) // (y-x)-y
					else if(op1 == "+" && arg0[0] == arg1[0]) AlgMult(AlgDouble(-1.0), arg1[1]) // y-(y+x) = -x
					else if(op1 == "+" && arg0[0] == arg1[1]) AlgMult(AlgDouble(-1.0), arg1[0]) // y-(x+y) = -x
					else if(op1 == "-" && arg0[0] == arg1[0]) arg1[1] // y-(y-x) = x
					else mAr;	
				}else mAr;
			}else mAr;
		}else mAr;
	});

	if(it == ar)
		it
	else
		foldExpressions(it);
} 

removeTrivialExp(ar:AlgExp) -> AlgExp {
	isZero = \ar0 -> getD(ar0).first && getD(ar0).second == 0.0;
	isOne = \ar0 -> getD(ar0).first && getD(ar0).second == 1.0;
	getVal = \ar0 -> getD(ar0).second;
	it = execSimplifier(ar, \op, mAr -> {
		args = extractArgs(mAr);
		if(length(args) == 2)
		{
			if(getD(args[0]).first && getD(args[1]).first)
				AlgDouble(convVals(op, getVal(args[0]), getVal(args[1])))
			else if(op == "*") {
				if(isZero(args[1]) || isZero(args[0]))//x*0 or 0*r
					zero()
				else if(isOne(args[1]))//x*1 or 1*x
					args[0]
				else if(isOne(args[0]))
					args[1]
				else mAr;	
			}else if(op == "/") {
				if(isZero(args[0]))// 0/x
					zero()
				else if(isOne(args[1]))// x/1
					args[0]
				else if(args[0] == args[1])// x/x
					one()
				else mAr;
			}else if(op == "-") {
				if(args[0] == args[1]) // x-x
					zero()
				else if(isZero(args[1]))// x-0
					args[0]
				else mAr;
			}else if(op == "+") {
				if(isZero(args[0]))// 0+x
					args[1]
				else if(isZero(args[1]))// x+0
					args[0]
				else if(args[0] == args[1]) // x+x = 2x
					AlgMult(two(), args[0])
				else mAr;	
			}else mAr;
		}else 
			mAr;	
	});	

	if(it == ar)
		it
	else
		removeTrivialExp(it);		
}

execSimplifier(m: AlgExp, condOp: (string, AlgExp) -> AlgExp) -> AlgExp {
	arr = extractArgs(m);

	if(length(arr) < 2)
		m
	else 
	{
		newL = detectAndReplace(arr[0], condOp);
		newR = detectAndReplace(arr[1], condOp);

		op = detectType(m);
		condOp(op, createStruct(op, newL, detectAndReplace(arr[1], condOp)));
	}
}

detectAndReplace(ar:AlgExp, condOp) -> AlgExp {
	switch(ar) {
		AlgDouble(val): 
			ar;
		AlgVar(var):
			ar;
		default:
			execSimplifier(ar, condOp);
	}
}

extractArgs(ar:AlgExp) -> [AlgExp] {
	switch(ar) {
		AlgDiv(up, down): 
			[up,down];
		AlgMin(l, r): 
			[l,r];
		AlgSum(l, r): 
			[l,r];
		AlgMult(l, r): 
			[l,r];
		default:
			[ar];
	}	
}

isAtom(a) {
	switch(a) {
		AlgDouble(val): true;
		AlgVar(var): true;
		default: false;
	}
}

isNegInt(a) {
	p = getD(a); 
	p.second < 0.0;
}

extractSign(ar:AlgExp) -> Pair<int, AlgExp> {
	arr = extractArgs(ar);

	if(length(arr) < 2)
	{
		if(getD(ar).first && getD(ar).second < 0.0)
			Pair(-1, AlgDouble(abs(getD(ar).second)))
		else
			Pair(1, ar);
	}else 
	{
		op = detectType(ar);

		newL = extractSign(arr[0]);
		newR = extractSign(arr[1]);
		signMul = \l,r -> if(l*r > 0) 1 else -1;

		if(op == "*" || op == "/") 
			Pair(signMul(newL.first, newR.first), createStruct(op, newL.second, newR.second))
		else if(op == "-") {
			if(newL.first < 0 && newR.first > 0) //-x -y = -1 * (x+y)
				Pair(-1, createStruct("+", newL.second, newR.second))
			else if(newL.first > 0 && newR.first < 0) //x - (-y) = x + y
				Pair(1, createStruct("+", newL.second, newR.second))
			else if(newL.first < 0 && newR.first < 0) //-x - (-y) = -x + y
				Pair(1, createStruct("+", AlgMult(AlgDouble(-1.0), newL.second), newR.second))
			else
				Pair(1, createStruct(op, newL.second, newR.second));	
		} else if(op == "+") {
			if(newL.first < 0 && newR.first < 0) //-x + (-y) = -1 * (x+y)
				Pair(-1, createStruct("+", newL.second, newR.second))
			else if(newL.first > 0 && newR.first < 0) // x + (-y) = x - y
				Pair(1, createStruct("-", newL.second, newR.second))
			else if(newL.first < 0 && newR.first > 0) // -x + y = y - x
				Pair(1, createStruct("-", newR.second, newL.second))
			else
				Pair(1, createStruct(op, newL.second, newR.second));
		}else
			Pair(1, createStruct(op, newL.second, newR.second));
	}
}

getD(a:AlgExp) -> Pair<bool, double> {
	switch (a) {
		AlgDouble(val):
			Pair(true, val);
		default:
			Pair(false, 5.0);
	}
}
		
detectType(ar: AlgExp) -> string {
	switch (ar) {
		AlgDiv(up, down): "/";
		AlgMin(l, r): "-";
		AlgSum(l, r): "+";
		AlgMult(l, r): "*";
		AlgDouble(val): "int";
		AlgVar(var): "variable";	
	}
}

convVals(op: string, d0: double, d1: double) -> double {
	if(op == "+")
	{
		d0+d1;
	}else if(op == "-")
	{
		d0-d1;
	}else if(op == "*")
	{
		d0*d1;
	}else if(op == "/")
	{
		d0/d1;
	}else
		d0;
}

createStruct(op: string, l:AlgExp, r:AlgExp) -> AlgExp {
	if(op == "+")
	{
		AlgSum(l,r);
	}else if(op == "-")
	{
		AlgMin(l,r);
	}else if(op == "*")
	{
		AlgMult(l,r);
	}else if(op == "/")
	{
		AlgDiv(l,r);
	}else
		l;	
}

zero() {
	AlgDouble(0.0)
}

one() {
	AlgDouble(1.0)
}

two() {
	AlgDouble(2.0)
}